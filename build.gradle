plugins {
    id 'java-library'
    id 'net.ltgt.errorprone' version '0.0.13'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'net.researchgate.release' version '2.6.0'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.guava:guava:23.4-jre'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.google.errorprone:error_prone_annotations:2.1.2'

    testImplementation 'org.testng:testng:6.11'
    testImplementation 'com.google.truth:truth:0.37'
    testImplementation 'com.google.truth.extensions:truth-java8-extension:0.37'
}

test {
    useTestNG()
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

shadowJar {
    dependencies {
        include(dependency('com.google.guava:guava:23.4-jre'))
    }
    relocate 'com.google', 'com.tripadvisor.reflow.repackage.com.google'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

javadoc {
    title 'Reflow API'
}

release {
    tagTemplate = 'v${version}'
}
